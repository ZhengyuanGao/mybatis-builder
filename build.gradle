plugins {
    id("java")
    id("org.jetbrains.intellij") version "1.17.3"
//    id "structure-intellij" version "3.264"
}

group 'com.chuntung.plugin'
version "${version}"

repositories {
    mavenLocal()
    maven { url "https://maven.aliyun.com/repository/public" }
    maven { url "https://repo.huaweicloud.com/repository/maven" }
    mavenCentral()
}

intellij {
    pluginName = "MyBatis Builder"
    version = ideaVersion
    plugins = ["java"]
    sameSinceUntilBuild = Boolean.valueOf(isEAP)
    patchPluginXml {
        untilBuild = customUtilBuild
        changeNotes = "${new File('changeNotes.txt').getText('UTF-8')}"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = "17"
    targetCompatibility = "17"
}

sourceSets {
    test {
        java {
            exclude '**/example'
        }
    }

    main {
        java {
            exclude '**/support'
        }
    }
}

dependencies {
    implementation( group: 'org.mybatis.generator', name: 'mybatis-generator-core', version: '1.4.2') {
        exclude group: 'org.junit.jupiter'
        exclude group: 'org.junit.platform'
        exclude group: 'org.assertj'
    }
    implementation group: 'com.github.javaparser', name: 'javaparser-core', version: '3.19.0'
    implementation group: 'org.postgresql', name: 'postgresql', version: '9.4.1212'
    implementation group: 'mysql', name: 'mysql-connector-java', version: '5.1.47'

    testImplementation('com.h2database:h2:1.4.200')
    testImplementation('org.mybatis:mybatis:3.5.9')
    testImplementation("org.mybatis.dynamic-sql:mybatis-dynamic-sql:1.5.0")
}
